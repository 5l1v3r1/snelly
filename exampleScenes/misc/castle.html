   
<body onload="onLoad();">
<script src="https://portsmouth.github.io/snelly/js/compiled/snelly.min.js"></script>
    
<?/* ------------------------------------------------------------------*/?>
<?/*                         scene definition                          */?>
<?/* ------------------------------------------------------------------*/?>

<script>

function Scene() {}

Scene.prototype.shader = function()
{
    return `
////////////////////////////////////////////////////////////////
//
//             HELPER FUNCTIONS/MACROS
//
////////////////////////////////////////////////////////////////

#define PI 3.14159265
#define TAU (2.0*PI)
#define PHI (sqrt(5.0)*0.5 + 0.5)

// Clamp to [0,1] - this operation is free under certain circumstances.
#define saturate(x) clamp(x, 0.0, 1.0)

// Sign function that doesn't return 0
float sgn(float x) {
	return (x<0.0)?-1.0:1.0;
}

vec2 sgn(vec2 v) {
	return vec2((v.x<0.0)?-1.0:1.0, (v.y<0.0)?-1.0:1.0);
}

float square (float x) {
	return x*x;
}

vec2 square (vec2 x) {
	return x*x;
}

vec3 square (vec3 x) {
	return x*x;
}

float lengthSqr(vec3 x) {
	return dot(x, x);
}


// Maximum/minumum elements of a vector
float vmax(vec2 v) {
	return max(v.x, v.y);
}

float vmax(vec3 v) {
	return max(max(v.x, v.y), v.z);
}

float vmax(vec4 v) {
	return max(max(v.x, v.y), max(v.z, v.w));
}

float vmin(vec2 v) {
	return min(v.x, v.y);
}

float vmin(vec3 v) {
	return min(min(v.x, v.y), v.z);
}

float vmin(vec4 v) {
	return min(min(v.x, v.y), min(v.z, v.w));
}

////////////////////////////////////////////////////////////////
//
//             PRIMITIVE DISTANCE FUNCTIONS
//
////////////////////////////////////////////////////////////////
//
// Conventions:
//
// Everything that is a distance function is called fSomething.
// The first argument is always a point in 2 or 3-space called <p>.
// Unless otherwise noted, (if the object has an intrinsic "up"
// side or direction) the y axis is "up" and the object is
// centered at the origin.
//
////////////////////////////////////////////////////////////////

float fSphere(vec3 p, float r) {
	return length(p) - r;
}

// Plane with normal n (n is normalized) at some distance from the origin
float fPlane(vec3 p, vec3 n, float distanceFromOrigin) {
	return dot(p, n) + distanceFromOrigin;
}

// Cheap Box: distance to corners is overestimated
float fBoxCheap(vec3 p, vec3 b) { //cheap box
	return vmax(abs(p) - b);
}

// Box: correct distance to corners
float fBox(vec3 p, vec3 b) {
	vec3 d = abs(p) - b;
	return length(max(d, vec3(0))) + vmax(min(d, vec3(0)));
}

// Same as above, but in two dimensions (an endless box)
float fBox2Cheap(vec2 p, vec2 b) {
	return vmax(abs(p)-b);
}

float fBox2(vec2 p, vec2 b) {
	vec2 d = abs(p) - b;
	return length(max(d, vec2(0))) + vmax(min(d, vec2(0)));
}


// Endless "corner"
float fCorner (vec2 p) {
	return length(max(p, vec2(0))) + vmax(min(p, vec2(0)));
}

// Blobby ball object. You've probably seen it somewhere. This is not a correct distance bound, beware.
float fBlob(vec3 p) {
	p = abs(p);
	if (p.x < max(p.y, p.z)) p = p.yzx;
	if (p.x < max(p.y, p.z)) p = p.yzx;
	float b = max(max(max(
		dot(p, normalize(vec3(1.0, 1.0, 1.0))),
		dot(p.xz, normalize(vec2(PHI+1.0, 1.0)))),
		dot(p.yx, normalize(vec2(1.0, PHI)))),
		dot(p.xz, normalize(vec2(1.0, PHI))));
	float l = length(p);
	return l - 1.5 - 0.2 * (1.5 / 2.0)* cos(min(sqrt(1.01 - b / l)*(PI / 0.25), PI));
}

// Cylinder standing upright on the xz plane
float fCylinder(vec3 p, float r, float height) {
	float d = length(p.xz) - r;
	d = max(d, abs(p.y) - height);
	return d;
}

// Capsule: A Cylinder with round caps on both sides
float fCapsule(vec3 p, float r, float c) {
	return mix(length(p.xz) - r, length(vec3(p.x, abs(p.y) - c, p.z)) - r, step(c, abs(p.y)));
}

// Distance to line segment between <a> and <b>, used for fCapsule() version 2below
float fLineSegment(vec3 p, vec3 a, vec3 b) {
	vec3 ab = b - a;
	float t = saturate(dot(p - a, ab) / dot(ab, ab));
	return length((ab*t + a) - p);
}

// Capsule version 2: between two end points <a> and <b> with radius r 
float fCapsule(vec3 p, vec3 a, vec3 b, float r) {
	return fLineSegment(p, a, b) - r;
}

// Torus in the XZ-plane
float fTorus(vec3 p, float smallRadius, float largeRadius) {
	return length(vec2(length(p.xz) - largeRadius, p.y)) - smallRadius;
}

// A circle line. Can also be used to make a torus by subtracting the smaller radius of the torus.
float fCircle(vec3 p, float r) {
	float l = length(p.xz) - r;
	return length(vec2(p.y, l));
}

// A circular disc with no thickness (i.e. a cylinder with no height).
// Subtract some value to make a flat disc with rounded edge.
float fDisc(vec3 p, float r) {
	float l = length(p.xz) - r;
	return l < 0.0 ? abs(p.y) : length(vec2(p.y, l));
}

// Hexagonal prism, circumcircle variant
float fHexagonCircumcircle(vec3 p, vec2 h) {
	vec3 q = abs(p);
	return max(q.y - h.y, max(q.x*sqrt(3.0)*0.5 + q.z*0.5, q.z) - h.x);
	//this is mathematically equivalent to this line, but less efficient:
	//return max(q.y - h.y, max(dot(vec2(cos(PI/3), sin(PI/3)), q.zx), q.z) - h.x);
}

// Hexagonal prism, incircle variant
float fHexagonIncircle(vec3 p, vec2 h) {
	return fHexagonCircumcircle(p, vec2(h.x*sqrt(3.0)*0.5, h.y));
}

// Cone with correct distances to tip and base circle. Y is up, 0 is in the middle of the base.
float fCone(vec3 p, float radius, float height) {
	vec2 q = vec2(length(p.xz), p.y);
	vec2 tip = q - vec2(0, height);
	vec2 mantleDir = normalize(vec2(height, radius));
	float mantle = dot(tip, mantleDir);
	float d = max(mantle, -q.y);
	float projected = dot(tip, vec2(mantleDir.y, -mantleDir.x));
	
	// distance to tip
	if ((q.y > height) && (projected < 0.0)) {
		d = max(d, length(tip));
	}
	
	// distance to base ring
	if ((q.x > radius) && (projected > length(vec2(height, radius)))) {
		d = max(d, length(q - vec2(radius, 0)));
	}
	return d;
}

////////////////////////////////////////////////////////////////
//
//                DOMAIN MANIPULATION OPERATORS
//
////////////////////////////////////////////////////////////////
//
// Conventions:
//
// Everything that modifies the domain is named pSomething.
//
// Many operate only on a subset of the three dimensions. For those,
// you must choose the dimensions that you want manipulated
// by supplying e.g. <p.x> or <p.zx>
//
// <inout p> is always the first argument and modified in place.
//
// Many of the operators partition space into cells. An identifier
// or cell index is returned, if possible. This return value is
// intended to be optionally used e.g. as a random seed to change
// parameters of the distance functions inside the cells.
//
// Unless stated otherwise, for cell index 0, <p> is unchanged and cells
// are centered on the origin so objects don't have to be moved to fit.
//
//
////////////////////////////////////////////////////////////////



// Rotate around a coordinate axis (i.e. in a plane perpendicular to that axis) by angle <a>.
// Read like this: R(p.xz, a) rotates "x towards z".
// This is fast if <a> is a compile-time constant and slower (but still practical) if not.
void pR(inout vec2 p, float a) {
	p = cos(a)*p + sin(a)*vec2(p.y, -p.x);
}

// Shortcut for 45-degrees rotation
void pR45(inout vec2 p) {
	p = (p + vec2(p.y, -p.x))*sqrt(0.5);
}

// Repeat space along one axis. Use like this to repeat along the x axis:
// <float cell = pMod1(p.x,5);> - using the return value is optional.
float pMod1(inout float p, float size) {
	float halfsize = size*0.5;
	float c = floor((p + halfsize)/size);
	p = mod(p + halfsize, size) - halfsize;
	return c;
}

// Same, but mirror every second cell so they match at the boundaries
float pModMirror1(inout float p, float size) {
	float halfsize = size*0.5;
	float c = floor((p + halfsize)/size);
	p = mod(p + halfsize,size) - halfsize;
	p *= mod(c, 2.0)*2.0 - 1.0;
	return c;
}

// Repeat the domain only in positive direction. Everything in the negative half-space is unchanged.
float pModSingle1(inout float p, float size) {
	float halfsize = size*0.5;
	float c = floor((p + halfsize)/size);
	if (p >= 0.0)
		p = mod(p + halfsize, size) - halfsize;
	return c;
}

// Repeat only a few times: from indices <start> to <stop> (similar to above, but more flexible)
float pModInterval1(inout float p, float size, float start, float stop) {
	float halfsize = size*0.5;
	float c = floor((p + halfsize)/size);
	p = mod(p+halfsize, size) - halfsize;
	if (c > stop) { //yes, this might not be the best thing numerically.
		p += size*(c - stop);
		c = stop;
	}
	if (c <start) {
		p += size*(c - start);
		c = start;
	}
	return c;
}

// Repeat around the origin by a fixed angle.
// For easier use, num of repetitions is use to specify the angle.
float pModPolar(inout vec2 p, float repetitions) {
	float angle = 2.0*PI/repetitions;
	float a = atan(p.y, p.x) + angle/2.;
	float r = length(p);
	float c = floor(a/angle);
	a = mod(a,angle) - angle/2.0;
	p = vec2(cos(a), sin(a))*r;
	// For an odd number of repetitions, fix cell index of the cell in -x direction
	// (cell index would be e.g. -5 and 5 in the two halves of the cell):
	if (abs(c) >= (repetitions/2.0)) c = abs(c);
	return c;
}

// Repeat in two dimensions
vec2 pMod2(inout vec2 p, vec2 size) {
	vec2 c = floor((p + size*0.5)/size);
	p = mod(p + size*0.5,size) - size*0.5;
	return c;
}

// Same, but mirror every second cell so all boundaries match
vec2 pModMirror2(inout vec2 p, vec2 size) {
	vec2 halfsize = size*0.5;
	vec2 c = floor((p + halfsize)/size);
	p = mod(p + halfsize, size) - halfsize;
	p *= mod(c,vec2(2.0))*2.0 - vec2(1.0);
	return c;
}

// Same, but mirror every second cell at the diagonal as well
vec2 pModGrid2(inout vec2 p, vec2 size) {
	vec2 c = floor((p + size*0.5)/size);
	p = mod(p + size*0.5, size) - size*0.5;
	p *= mod(c,vec2(2.0))*2.0 - vec2(1.0);
	p -= size/2.0;
	if (p.x > p.y) p.xy = p.yx;
	return floor(c/2.0);
}

// Repeat in three dimensions
vec3 pMod3(inout vec3 p, vec3 size) {
	vec3 c = floor((p + size*0.5)/size);
	p = mod(p + size*0.5, size) - size*0.5;
	return c;
}

// Mirror at an axis-aligned plane which is at a specified distance <dist> from the origin.
float pMirror (inout float p, float dist) {
	float s = sgn(p);
	p = abs(p)-dist;
	return s;
}

// Mirror in both dimensions and at the diagonal, yielding one eighth of the space.
// translate by dist before mirroring.
vec2 pMirrorOctant (inout vec2 p, vec2 dist) {
	vec2 s = sgn(p);
	pMirror(p.x, dist.x);
	pMirror(p.y, dist.y);
	if (p.y > p.x)
		p.xy = p.yx;
	return s;
}

// Reflect space at a plane
float pReflect(inout vec3 p, vec3 planeNormal, float offset) {
	float t = dot(p, planeNormal)+offset;
	if (t < 0.0) {
		p = p - (2.0*t)*planeNormal;
	}
	return sgn(t);
}

////////////////////////////////////////////////////////////////
//
//             OBJECT COMBINATION OPERATORS
//
////////////////////////////////////////////////////////////////
//
// We usually need the following boolean operators to combine two objects:
// Union: OR(a,b)
// Intersection: AND(a,b)
// Difference: AND(a,!b)
// (a and b being the distances to the objects).
//
// The trivial implementations are min(a,b) for union, max(a,b) for intersection
// and max(a,-b) for difference. To combine objects in more interesting ways to
// produce rounded edges, chamfers, stairs, etc. instead of plain sharp edges we
// can use combination operators. It is common to use some kind of "smooth minimum"
// instead of min(), but we don't like that because it does not preserve Lipschitz
// continuity in many cases.
//
// Naming convention: since they return a distance, they are called fOpSomething.
// The different flavours usually implement all the boolean operators above
// and are called fOpUnionRound, fOpIntersectionRound, etc.
//
// The basic idea: Assume the object surfaces intersect at a right angle. The two
// distances <a> and <b> constitute a new local two-dimensional coordinate system
// with the actual intersection as the origin. In this coordinate system, we can
// evaluate any 2D distance function we want in order to shape the edge.
//
// The operators below are just those that we found useful or interesting and should
// be seen as examples. There are infinitely more possible operators.
//
// They are designed to actually produce correct distances or distance bounds, unlike
// popular "smooth minimum" operators, on the condition that the gradients of the two
// SDFs are at right angles. When they are off by more than 30 degrees or so, the
// Lipschitz condition will no longer hold (i.e. you might get artifacts). The worst
// case is parallel surfaces that are close to each other.
//
// Most have a float argument <r> to specify the radius of the feature they represent.
// This should be much smaller than the object size.
//
// Some of them have checks like "if ((-a < r) && (-b < r))" that restrict
// their influence (and computation cost) to a certain area. You might
// want to lift that restriction or enforce it. We have left it as comments
// in some cases.
//
// usage example:
//
// float fTwoBoxes(vec3 p) {
//   float box0 = fBox(p, vec3(1));
//   float box1 = fBox(p-vec3(1), vec3(1));
//   return fOpUnionChamfer(box0, box1, 0.2);
// }
//
////////////////////////////////////////////////////////////////


// The "Chamfer" flavour makes a 45-degree chamfered edge (the diagonal of a square of size <r>):
float fOpUnionChamfer(float a, float b, float r) {
	return min(min(a, b), (a - r + b)*sqrt(0.5));
}

// Intersection has to deal with what is normally the inside of the resulting object
// when using union, which we normally don't care about too much. Thus, intersection
// implementations sometimes differ from union implementations.
float fOpIntersectionChamfer(float a, float b, float r) {
	return max(max(a, b), (a + r + b)*sqrt(0.5));
}

// Difference can be built from Intersection or Union:
float fOpDifferenceChamfer (float a, float b, float r) {
	return fOpIntersectionChamfer(a, -b, r);
}

// The "Round" variant uses a quarter-circle to join the two objects smoothly:
float fOpUnionRound(float a, float b, float r) {
	vec2 u = max(vec2(r - a,r - b), vec2(0));
	return max(r, min (a, b)) - length(u);
}

float fOpIntersectionRound(float a, float b, float r) {
	vec2 u = max(vec2(r + a,r + b), vec2(0));
	return min(-r, max (a, b)) + length(u);
}

float fOpDifferenceRound (float a, float b, float r) {
	return fOpIntersectionRound(a, -b, r);
}

// The "Columns" flavour makes n-1 circular columns at a 45 degree angle:
float fOpUnionColumns(float a, float b, float r, float n) {
	if ((a < r) && (b < r)) {
		vec2 p = vec2(a, b);
		float columnradius = r*sqrt(2.0)/((n-1.0)*2.0+sqrt(2.0));
		pR45(p);
		p.x -= sqrt(2.0)/2.0*r;
		p.x += columnradius*sqrt(2.0);
		if (mod(n,2.0) == 1.0) {
			p.y += columnradius;
		}
		// At this point, we have turned 45 degrees and moved at a point on the
		// diagonal that we want to place the columns on.
		// Now, repeat the domain along this direction and place a circle.
		pMod1(p.y, columnradius*2.0);
		float result = length(p) - columnradius;
		result = min(result, p.x);
		result = min(result, a);
		return min(result, b);
	} else {
		return min(a, b);
	}
}

float fOpDifferenceColumns(float a, float b, float r, float n) {
	a = -a;
	float m = min(a, b);
	//avoid the expensive computation where not needed (produces discontinuity though)
	if ((a < r) && (b < r)) {
		vec2 p = vec2(a, b);
		float columnradius = r*sqrt(2.0)/n/2.0;
		columnradius = r*sqrt(2.0)/((n-1.0)*2.0+sqrt(2.0));

		pR45(p);
		p.y += columnradius;
		p.x -= sqrt(2.0)/2.0*r;
		p.x += -columnradius*sqrt(2.0)/2.0;

		if (mod(n,2.0) == 1.0) {
			p.y += columnradius;
		}
		pMod1(p.y,columnradius*2.0);

		float result = -length(p) + columnradius;
		result = max(result, p.x);
		result = min(result, a);
		return -min(result, b);
	} else {
		return -m;
	}
}

float fOpIntersectionColumns(float a, float b, float r, float n) {
	return fOpDifferenceColumns(a,-b,r, n);
}

// The "Stairs" flavour produces n-1 steps of a staircase:
// much less stupid version by paniq
float fOpUnionStairs(float a, float b, float r, float n) {
	float s = r/n;
	float u = b-r;
	return min(min(a,b), 0.5 * (u + a + abs ((mod (u - a + s, 2.0 * s)) - s)));
}

// We can just call Union since stairs are symmetric.
float fOpIntersectionStairs(float a, float b, float r, float n) {
	return -fOpUnionStairs(-a, -b, r, n);
}

float fOpDifferenceStairs(float a, float b, float r, float n) {
	return -fOpUnionStairs(-a, b, r, n);
}


// Similar to fOpUnionRound, but more lipschitz-y at acute angles
// (and less so at 90 degrees). Useful when fudging around too much
// by MediaMolecule, from Alex Evans' siggraph slides
float fOpUnionSoft(float a, float b, float r) {
	float e = max(r - abs(a - b), 0.0);
	return min(a, b) - e*e*0.25/r;
}


// produces a cylindical pipe that runs along the intersection.
// No objects remain, only the pipe. This is not a boolean operator.
float fOpPipe(float a, float b, float r) {
	return length(vec2(a, b)) - r;
}

// first object gets a v-shaped engraving where it intersect the second
float fOpEngrave(float a, float b, float r) {
	return max(a, (a + r - abs(b))*sqrt(0.5));
}

// first object gets a capenter-style groove cut out
float fOpGroove(float a, float b, float ra, float rb) {
	return max(a, min(a + ra, rb - abs(b)));
}

// first object gets a carpenter-style tongue attached
float fOpTongue(float a, float b, float ra, float rb) {
	return min(a, max(a - ra, abs(b) - rb));
}

//////////////////////////////////////

vec3 hash3( vec2 p ){
    vec3 q = vec3( dot(p,vec2(127.1,311.7)), 
				   dot(p,vec2(269.5,183.3)), 
				   dot(p,vec2(419.2,371.9)) );
	return fract(sin(q)*43758.5453);
}

//////////////////////////////////////

    uniform float a;
    uniform float b;
    uniform float c;
    uniform float d;
    uniform float e;
    uniform float f;
    uniform float g;
    uniform float h;
    uniform float i;
    uniform float j;

    uniform float X;
    uniform float Y;
    uniform float Z;
    uniform float W;

    float wall_thickness;
    float floor_height;
    float buildingWidth;
    float numFloors;
    float buildingHeight;
    float window_height;
    float window_width;

    void INIT()
    {
        floor_height = 20.0 * Y;
        wall_thickness = 0.12*floor_height;
        numFloors = 4.0;
        buildingHeight = numFloors * floor_height;
        window_height = floor_height/3.5;
        window_width = window_height/0.7;
        buildingWidth = 3.0*W*floor_height;
    }

    float level(vec3 p)
    {
        // repeat in z-axis to generate wall of windows
        float windowSpacing = a*floor_height;
        float cell = pModMirror1(p.z, b*windowSpacing);
        float wall = fBox2(p.xy, vec2(0.5*wall_thickness, 0.5*floor_height));
        vec3 q = p;
        q.z += 0.6*window_width;                 // stagger windows
        q.z = abs(q.z) + 0.25*window_width;      // squeeze window shape

        // define window shape
        float box = fBox(q, vec3(0.5*wall_thickness, 0.5*window_height, 0.5*window_width));
        q.y -= 0.5*window_height;
        float cylinder = fCylinder(q.yxz, 0.5*window_width, wall_thickness);
        float window = min(box, cylinder);
        wall = fOpDifferenceStairs(wall, window, wall_thickness*0.666, 3.0);

        // floor
        q = p; 
        q.y -= 0.5*floor_height;
        q.x -= 0.5*buildingWidth;
        float floor = fBox2(q.xy, vec2(0.5*buildingWidth, 0.5*wall_thickness));

        // ceiling
        q = p;
        q.y += 0.5*floor_height;
        q.x -= 0.5*buildingWidth;
        float ceiling = fBox2(q.xy, vec2(0.5*buildingWidth, 0.5*wall_thickness));

        float level = min(min(wall, floor), ceiling);
        return level;
    }

    float building(vec3 X)
    {
        vec3 p = X;

        // reflect to generate two walls
        p.x = buildingWidth - abs(p.x);
    
        // battlements
        float crenellation_width = 0.333 * floor_height;
        vec3 q = p;
        q.y -= buildingHeight + window_height + wall_thickness + 0.75*crenellation_width;
        float battlement_wall = fBox2(q.xy, vec2(0.75*wall_thickness, 0.5*crenellation_width));
        q.y -= crenellation_width;
        pMod1(q.z, 2.0*crenellation_width);
        float crenellations = fBox(q, vec3(0.5*wall_thickness, 0.5*crenellation_width, 0.5*crenellation_width));
        float battlements = min(crenellations, battlement_wall);

        // repeat in y-axis to generate floors
        pModInterval1(p.y, floor_height, 0.9, numFloors);
        float floors = level(p);

        float building = min(floors, battlements);
        return building;
    }

    float scene(vec3 p)
    {
        vec2 cell_0 = pModGrid2(p.xz, 50.0*vec2(g, h));
        pModPolar(p.xz, i);
        p.xz -= cell_0;
        vec2 mirror_0 = pMirrorOctant(p.xz, vec2(100.0*c, 100.0*d));
        p.xz -= mirror_0;
        vec2 mirror_1 = pMirrorOctant(p.xz, vec2(100.0*e, 100.0*f));
        p.xz -= mirror_1;
        return 0.5*building(p);
    }


    float SDF_SURFACE(vec3 p)
    {
        float L = 1.0e4;
        vec3 q = p;
        q.y -= 5.0*wall_thickness;
        float floor = fBox(q, vec3(L, 1.0,L));
        return min(scene(p), floor);
    }



    vec3 SURFACE_DIFFUSE_REFLECTANCE(in vec3 C, in vec3 p, in vec3 N, in vec3 V)
    {
        return C;
    }
    `;
}

Scene.prototype.init = function(snelly)
{
    let renderer  = snelly.getRenderer();
let camera    = snelly.getCamera();
let controls  = snelly.getControls();
let materials = snelly.getMaterials();
    
/*
this.par = {};
this.par.a = 1;
this.par.b = 2.1;
this.par.c = 0.6765327695560254;
this.par.d = 3.7209302325581395;
this.par.e = 1;
this.par.f = 1;
this.par.g = 4.688408379123453;
this.par.h = 5.299506694855532;
this.par.i = 1.4002713025648714;
this.par.j = 1;
this.par.X = 1;
this.par.Y = 1;
this.par.W = 1;
this.par.Z = 1;
*/

this.par = {};
this.par.a = 2.8186386426338266;
this.par.b = 2.480402005517768;
this.par.c = 3.2;
this.par.d = 2.480402005517768;
this.par.e = 1.8039287312856491;
this.par.f = 3.720603008276652;
this.par.g = 7.6666971079640085;
this.par.h = 4.171585191098064;
this.par.i = 1.4002713025648714;
this.par.j = 1;
this.par.X = 1;
this.par.Y = 1;
this.par.W = 1;
this.par.Z = 1;
    
    
snelly.showGUI(true);

/** Camera settings **/
camera.fov = 45;
camera.aperture = -12.13305964742817;
camera.focalDistance = 2.7997973274886556;
camera.up.set(0, 1, 0);
camera.position.set(4319.567481138579, 461.2879088478121, -3469.763406782769);
controls.target.set(1191.12086752288, -2648.6304745010993, -36.630484892214156);

/** Renderer settings **/
renderer.renderMode = 'ao';
renderer.maxSamplesPerFrame = 1;
renderer.maxBounces = 1;
renderer.maxScatters = 2;
renderer.maxMarchSteps = 500;
renderer.maxVolumeSteps = 256;
renderer.maxStepsIsMiss = false;
renderer.interactive = true;
renderer.goalFPS = 20;
renderer.minsSPPToRedraw = 0;
renderer.radianceClamp = 12;
renderer.wavelengthSamples = 1024;
renderer.exposure = 0.0;
renderer.gamma = 1.5293265749456915;
renderer.contrast = 1.3;
renderer.saturation = 1.3207820419985516;
renderer.skyPower = 10;
renderer.skyTemperature = 6000;
renderer.envMapRotation = 263.31432392770205;
renderer.envMapVisible = true;
renderer.sunPower = -10;
renderer.sunColor = [0.7843137254901961, 0.7598887305787367, 0.5767012687427913];
renderer.sunAngularSize = 9.371428571428572;
renderer.sunLatitude = 39.56736574220261;
renderer.sunLongitude = 148.74513508147137;
renderer.sunVisibleDirectly = true;
renderer.shadowStrength = 1;

/** Material settings **/
let surface = materials.loadSurface();
surface.roughness = 0.009018057532952543;
surface.ior = 1.2930868698209577;
surface.diffuseAlbedo = [0.8333205676497498, 0.8044502382813904, 0.7516162151899565];
surface.specAlbedo = [0.6176177728435437, 0.6176177728435437, 0.6176177728435437];
}


Scene.prototype.getName = function() { return 'Hello World'; }
Scene.prototype.getURL = function() { return "https://github.com/portsmouth/snelly/blob/master/exampleScenes/misc/helloWorld.html"; }
Scene.prototype.getLengthScale = function()    { return 1.0; }
Scene.prototype.getMinLengthScale = function() { return 1.0e-3 * this.getLengthScale(); }
Scene.prototype.getMaxLengthScale = function() { return 5.0e3 * this.getLengthScale(); }
Scene.prototype.initGui = function(gui) 
{ 
    gui.addSlider(this.par, {name: 'a', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'b', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'c', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'd', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'e', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'f', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'g', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'h', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'i', min: 0.0, max: 16.0}); 
    gui.addSlider(this.par, {name: 'j', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'X', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'Y', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'W', min: 0.0, max: 10.0}); 
    gui.addSlider(this.par, {name: 'Z', min: 0.0, max: 10.0}); 
}
Scene.prototype.syncShader = function(snelly, shader) 
{ 
    shader.uniformF("a", this.par.a);
    shader.uniformF("b", this.par.b);
    shader.uniformF("c", this.par.c);
    shader.uniformF("d", this.par.d);
    shader.uniformF("e", this.par.e); 
    shader.uniformF("f", this.par.f);
    shader.uniformF("g", this.par.g);
    shader.uniformF("h", this.par.h);
    shader.uniformF("i", this.par.i);
    shader.uniformF("j", this.par.j);
    shader.uniformF("X", this.par.X);
    shader.uniformF("Y", this.par.Y);
    shader.uniformF("Z", this.par.Z);
    shader.uniformF("W", this.par.W);
}
Scene.prototype.initGenerator = function()
{
    return `
this.par = {};
this.par.a = ${this.par.a};
this.par.b = ${this.par.b};
this.par.c = ${this.par.c};
this.par.d = ${this.par.d};
this.par.e = ${this.par.e};
this.par.f = ${this.par.f};
this.par.g = ${this.par.g};
this.par.h = ${this.par.h};
this.par.i = ${this.par.i};
this.par.j = ${this.par.j};
this.par.X = ${this.par.X};
this.par.Y = ${this.par.Y};
this.par.W = ${this.par.W};
this.par.Z = ${this.par.Z};
    `;
}


</script>

<?/* ------------------------------------------------------------------*/?>
<?/*                            main loop                              */?>
<?/* ------------------------------------------------------------------*/?>

<script type="text/javascript">
function onLoad() { snelly = new Snelly(new Scene()); animateLoop(); }
function animateLoop() { snelly.render(); window.requestAnimationFrame(animateLoop); }
</script>

</body>
